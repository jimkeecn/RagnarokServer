

//===== rAthena Script =======================================
//= Curator Historica
//===== Description: =========================================
//Buy items from player for exchange of cash point.
//Transparent Plate 7352 7353 7354 7355 (research for  Juperos Ruins Center.)
//Chivalry Emblem 1004 (find king and slayer demo in Glast Heim Chivalry )
//Will of the Darkness  7340(Rachel Sanctuary)
//Twin Horn Helm or Single Horned Helmet  6323,6324(Kamidal Tunnel or Scaraba Hole)
//===== Changelogs: ==========================================
//= 1.0 First version [jimkeecn]
//= 1.1 Add Quest icon, Add More background, Add Level Checking [jimkeecn]
//= 2.0 Add Curator Historica Hunting Quest, reformat the dialog. [jimkeecn]
//============================================================

moc_para01,33,34,7	script	Curator Historica#eden	2_M_SAGE_OLD,{
    
    cutin "verus_aures",2;
    mes "[Curator Historica]";
    mes "Greetings, adventurer! I am known as Curator Historica, the gatherer of artifacts for global research.";
    next;
    mes "I work with renowned research agencies around the world to push the boundaries of historical knowledge.";
    mes "We offer^FF0000 cash points ^000000in exchange for high-value artifacts.";
    next;

    cutin "", 255;
    menu "About those artifacts.",L_Info,"I want to trade my artifacts.", L_Sell,"Tell me about the hunting quest.",L_Hunting,"I am not interested",L_NotInterested;

    
    L_NotInterested:
        mes "I hope to see you again when you are ready to contribute to our research.";
        close;
        end;

    L_Info:
        while (1){
        .@loop = 0;
        mes "[Curator Historica]";
        mes "we are seeking the following 3 artifacts from you.";
        next;
        while(.@loop == 0){
            switch(select("Transparent Plate:Will of the Darkness:Horned Helmet:Leave.")){
                case 1:
                    mes "^FF0000Transparent Plate^000000";
                    mes "There are four types of Transparent Plates, each made from different crystalline materials found deep within Juperos Ruins.";
                    next;
                    mes "These plates are believed to be part of the ancient technology used by the lost civilization of Juperos to enhance their robotic creations.";
                    mes "Researchers are eager to study their composition to understand more about ancient technologies and their applications.";
                    next;
                    set .@loop,1;
					break;
                case 2:
                    mes "^FF0000Will of the Darkness^000000";
                    mes "This artifact is said to contain the essence of dark power that once seeped through Rachel Sanctuary.";
                    mes "It is of great interest because it holds clues to the ancient curse that fell upon this sacred place, and understanding it could unlock secrets of ancient spiritual practices and divine worship.";
                    next;
                    set .@loop,1;
					break;
                case 3:
                    mes "^FF0000Horned Helmet^000000";
                    mes "These helmets, whether twin-horn or single-horn, are relics from the fierce Scaraba warriors of the Scaraba Hole.";
                    next;
                    mes "They are not only rare armaments but also significant cultural artifacts, offering insights into the battle gear and warrior culture of an almost forgotten subterranean race.";
                    next;
                    set .@loop,1;
					break;
                case 4:
                    close;
                    end;
                   
            }
        }
    }
        
        
    L_Hunting:

        .@quest_status = checkquest(100002,Hunting);
        if (.@quest_status == 0 || .@quest_status == 1){
            callsub L_Hunting_Inc;
            end;
        } else if(.@quest_status == 2){
            callsub L_Hunting_C;
            end;
        }

        .@sleep_quest_status = checkquest(100001,PLAYTIME);
        if (.@sleep_quest_status == 0 || .@sleep_quest_status == 1){
            callsub L_Hunting_Sleep;
            end;
        }
        mes "[Curator Historica]";
        mes "In our pursuit to expand the annals of Midgard's history, we've stumbled upon an intriguing research opportunity.";
        mes "Recent research suggests that the behaviors of certain species across the continent mirror historical cycles significant to Midgard's past. ";
        mes "These patterns could reveal insights into historical events that shaped our world";
        next;
        mes "You are tasked to engage with these creatures to gather critical data for our studies.";
        mes "The creatures in question are:";
        mes "^FF0000Scaraba, Dimik, Agav^000000.";
        next;
        mes "We'll reward you greatly for your efforts and the risks involved.";
        mes "Do you want to accept this quest?";
        next;
        switch(select("Yes.:No.")){
            case 1:
                if(BaseLevel < .minL){
                    mes "[Curator Historica]";
                    mes "You do not match with our requirement.";
                    mes "Please come back with a little more stronger.";
                    mes "required level ^FF0000" + .minL;
                    close;
                }

                if(BaseLevel > .maxL){
                    mes "[Curator Historica]";
                    mes "We no longer require your service, we have gained massive progress on our research.";
                    close;
                }
                
                mes "[Curator Historica]";
                mes "Thanks for accepting the challenge since the area are dangerous even for experience adventure like you.";
                mes "You can check out the area that list in my artifacts list.";
                mes "Wish you best luck";
                setquest 100002;
                close;
                end;




            case 2:
                mes "[Curator Historica]";
                mes "I hope to see you again when you are ready to contribute to our research.";
                close;
        }

    L_Hunting_Inc:
        mes "[Curator Historica]";
        mes "Looks like you have not yet fullfilled your duty. Please comeback after finish your report.";
        close;

    L_Hunting_C:
        mes "[Curator Historica]";
		mes "Good work~!!";
		mes "Our researchers are so happy with your work. they will appreciate what you have done.";
        close2;
        erasequest 100002;
		setquest 100001;
        #CASHPOINTS += 400;
        end;     

    L_Hunting_Sleep:
        mes "[Curator Historica]";
        mes "Sorry, We are currently reading your last report. Please come back after sometime..";
        close; 

    L_Sell:

        if(BaseLevel < .minL){
            mes "[Curator Historica]";
            mes "You do not match with our requirement.";
            mes "Please come back with a little more stronger.";
            mes "required level ^FF0000" + .minL;
            close;
        }

        if(BaseLevel > .maxL){
            mes "[Curator Historica]";
            mes "We no longer require your items, we have gained massive progress on our research.";
            close;
        }

        .@menu$ = "Transparent Plate:Will of the Darkness:Scabara Horned Helmet:close";

        mes "[Curator Historica]";
        mes "Please select the which artifacts you are willing to sell.";
        next;

            switch(select(.@menu$)){
                case 1:
                    .@rqamount = 5;
                    .@submenu$ = "Transparent Plate(7352):Transparent Plate(7353):Transparent Plate(7354):Transparent Plate(7355)";
                    mes "[Curator Historica]";
                    mes "We will buy ^FF0000" + .@rqamount + "^000000 of them for 1 cashpoint.";
                    mes "Please select which type of Transparent Plate you wish to sell.";
                    next;
                    switch(select(.@submenu$)){
                        case 1:
                            callsub S_GiveReward,7352,.@rqamount;
                            end;
                        case 2:
                            callsub S_GiveReward,7353,.@rqamount;
                            end;
                        case 3:
                            callsub S_GiveReward,7354,.@rqamount;
                            end;
                        case 4:
                            callsub S_GiveReward,7355,.@rqamount;
                            end;
                    }

                case 2:
                    .@rqamount = 30;
                    mes "[Curator Historica]";
                    mes "We will buy ^FF0000" + .@rqamount + "^000000 of them for 1 cashpoint.";
                    next;
                    callsub S_GiveReward,7340,.@rqamount;
                    end;
                case 3:
                    .@rqamount = 30;
                    .@submenu2$ = "Twin Horned Helmet:Single Horned Helmet";
                    mes "[Curator Historica]";
                    mes "We will buy ^FF0000" + .@rqamount + "^000000 of them for 1 cashpoint.";
                    mes "Please select which Scabara horn you wish to sell.";
                    next;
                    switch(select(.@submenu2$)){
                        case 1:
                            callsub S_GiveReward,6323,.@rqamount;
                            end;
                        case 2:
                            callsub S_GiveReward,6324,.@rqamount;
                            end;
                    }
                case 4:
                    mes "[Curator Historica]";
                    mes "See you next time.";
                    close;
                    end;
            }
        end;

    S_GiveReward:
        .@id = getarg(0);          // Item ID
        .@rqamount = getarg(1);    // Required amount of the item for one cash point
        .@amount = countitem(.@id); // Amount of the item the player has

        
        if (.@amount < .@rqamount) {
            mes "[Curator Historica]";
            mes "You don't have enough of the required item to claim the reward.";
            close;
        }


        // Calculate how many cash points the player can get
        .@cashpoints = .@amount / .@rqamount; // Integer division to get full cash points
        .@removable_amount = .@cashpoints * .@rqamount; // Calculate total artifacts to be removed

        .@confirm = select("Do you wish to sell " + .@removable_amount + " of " + getitemname(.@id) + ":Sorry, I Change my mind.");

        if(.@confirm > 1){
            mes "I hope to see you again when you are ready to contribute to our research.";
            close;
        }
        // Remove the calculated amount of item from player
        delitem .@id, .@removable_amount;

        // Add corresponding cash points to the player's account
        #CASHPOINTS += .@cashpoints;

        // Inform the player of the transaction
        mes "[Curator Historica]";
        mes "You have exchanged " + .@removable_amount + " of " + getitemname(.@id) + " for " + .@cashpoints + " cash points.";
        close;
        end;
    
    OnInit:
        .maxL = 150;
        .minL = 90;
        .@quest_c1$ = "checkquest(100002) == -1 && BaseLevel >= " + .minL + " && BaseLevel <= " + .maxL;
        questinfo QTYPE_QUEST, QMARK_NONE, .@quest_c1$;
        end;

}


//moc_para01,33,34,5	duplicate(curator_historica_lv150max)	Curator Historica#eden	57


